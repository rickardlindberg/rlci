Parser {
    file = block*:xs space !. -> xs
    block =
        | space 'par' space '{' block*:xs space '}'
            -> ["Par" ~xs]
        | space 'seq' space '{' block*:xs space '}'
            -> ["Seq" ~xs]
        | space 'template' space name:x space '{' block*:xs space '}'
            -> ["Template" x ~xs]
        | space 'link' space '(' space name:x space name:y ')'
            -> ["Link" x y]
        | space 'stage' space (name | -> ""):x space '{' stageBody*:ys space '}'
            -> ["Stage" x ~ys]
        | space name:x space '(' arg*:xs space ')'
            -> ["ApplyTemplate" x ~xs]
    stageBody =
        | space 'name' !alphanum space string:x
            -> ["StageName" x]
    arg = space name:x '=' string:y -> ["Arg" x y]
    string = '"' innerString*:xs '"' -> ["String" ~xs]
    innerString =
        | '#{' name:x '}' -> ["TemplateVar" x]
        | !'"' .:x        -> ["Char" x]
    name = alpha:x alphanum*:xs -> { x xs }
    alphanum = alpha | num
    alpha = 'a'-'z' | 'A'-'Z'
    num = '0'-'9'
    space = (' ' | '\n')*
}

ToDag {
    asts = ast*:xs !.
        -> dict():templates
        -> dict():stages
        -> []:nodes
        -> []:links
        -> []:parents
        -> xs
        -> [~nodes ~links]
    statements = ast*:xs
    ast = [%:x] -> x
    Par = ast*:xs -> [~~xs]
    Seq =
        | ast:x Seq:y -> x:parents -> y
        | ast:x !.    -> x
    Template = .:name .*:xs -> xs:x -> set(templates name x) -> x
    ApplyTemplate = .:name ast*:xs
        -> dict():templateArgs
        -> xs
        -> run("statements" get(templates name))
    String = ast*:xs -> { xs }
    Char = .
    TemplateVar = .:name -> get(templateArgs name)
    Arg = .:name ast:value -> set(templateArgs name value)
    Stage = .:varName ast*:props
        -> label():id
        -> dict():stageProps
        -> set(stageProps "name" { id })
        -> props
        -> set(stages varName id)
        -> add(nodes ["Node" id get(stageProps "name")])
        -> run("link" parents)
        -> [id]
    StageName = ast:name -> set(stageProps "name" name)
    Link = .:parent .:child
        -> add(links ["Link" get(stages parent) get(stages child)])
    link = (.:parent -> add(links ["Link" parent id]))*
}

ToDot {
    asts = ast*:xs !.       -> { "Digraph {\n" > xs < "}\n" }
    ast  = [%:x]            -> x
    Node = .:id .:name      -> { id "[label=\"" name "\"];\n" }
    Link = .:parent .:child -> { parent " -> " child ";\n" }
}
