Parser {
    file = statement*:xs space !. -> xs
    statement =
        | space 'var' !alphanum space name:x space '=' block:y
            -> ["Var" x y]
        | block
    block =
        | space 'par' space '{' statement*:xs space '}'
            -> ["Par" ~xs]
        | space 'seq' space '{' statement*:xs space '}'
            -> ["Seq" ~xs]
        | space 'template' space '{' statement*:xs space '}'
            -> ["Template" ~xs]
        | space 'link' space '(' space name:x space name:y ')'
            -> ["Link" x y]
        | space 'stage' space '(' string:x space ')'
            -> ["Stage" x]
        | space name:x space '(' space ')'
            -> ["ApplyTemplate" x]
    string = '"' (!'"'.)*:xs '"' -> { xs }
    name = alpha:x alphanum*:xs -> { x xs }
    alphanum = alpha | num
    alpha = 'a'-'z' | 'A'-'Z'
    num = '0'-'9'
    space = (' ' | '\n')*
}

ToDag {
    asts = ast*:xs !.
        -> dict():vars
        -> []:nodes
        -> []:links
        -> []:parents
        -> xs
        -> [~nodes ~links]
    statements = ast*:xs
    ast = [%:x] -> x
    Par = ast*:xs -> [~~xs]
    Seq =
        | ast:x Seq:y -> x:parents -> y
        | ast:x !.    -> x
    Template = .*
    Var = .:name ast:value -> value:x -> set(vars name x) -> x
    ApplyTemplate = .:name -> run("statements" get(vars name))
    Stage = .:name
        -> label():id
        -> add(nodes ["Node" id name])
        -> run("link" parents)
        -> [id]
    Link = .:parent .:child
        -> add(links ["Link" get(vars parent) get(vars child)])
    link = (.:parent -> add(links ["Link" parent id]))*
}

ToDot {
    asts = ast*:xs !.       -> { "Digraph {\n" > xs < "}\n" }
    ast  = [%:x]            -> x
    Node = .:id .:name      -> { id "[label=\"" name "\"];\n" }
    Link = .:parent .:child -> { parent " -> " child ";\n" }
}
